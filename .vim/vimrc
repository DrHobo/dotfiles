"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Matt's Vim Configuration File
"   ':help <command>' for help
"   ':set all?' to see currently set configuration settings
"   ':set all&' to set default settings
"   ':source $MYVIMRC' to update configuration changes
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
 
" Preamble Load Settings {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"- Load default $VIMRUNTIME/syntax/syntax.vim
syntax enable
"- Load built-in plugins
silent! packadd cfilter  " 'Cfilter /PAT/' to filter quickfix list
"- Load custom plugins
let pluginspath = "~/.vim/vimrc_plugins"
if filereadable(expand(pluginspath))
  execute "source " . pluginspath
endif
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1

" Basic Settings {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"- General
set backspace=indent,eol,start  " backspacing over everything in insert mode
set nobackup                    " no backup files
set colorcolumn=80,100          " highlight columns
set hidden                      " change buffers without saving
set lazyredraw                  " wait to draw result of repeated macros
set linebreak                   " linewrap full words
set history=1000                " keep # lines of command line history
set hlsearch                    " highlight matches
set incsearch                   " highlight search while typing
set matchpairs+=<:>             " match < > with %
set mouse=n                     " enable use of mouse in normal mode
set number                      " line numbers; use with relativenumber
set relativenumber              " relative line numbers
set showcmd                     " display commands as they are typed
set spelllang=en_us             " US dictionary for spell check
set nostartofline               " stay in column when C-d, C-u, C-b, C-f
set noswapfile                  " no swap files
set viminfo-=<50,s10            " save large yanks after close
set nowritebackup               " no backup before writing file
 
"- Command prompt completion
set wildignore=*.o,*~,*.pyc,*.dep,*.png,*.jpg,*.gif,*.swp
set wildmode=longest,list  " complete common match then show available
 
"- Searching
set ignorecase  " use with smartcase
set smartcase   " foo matches Foo and FOO; Foo matches Foo
 
"- Status line
set laststatus=2  " always show status line in last window
set statusline=[%n]\ %f\ %h%w%m%r\ %=%y\ %(%l,%c\ %=\ %P%)

"- Tab with spaces
set autoindent     " copy indent from current line to new line
set expandtab      " spaces instead of literal tab
set tabstop=4      " number of spaces in a tab
set shiftround     " round to tab indent level with '<' and '>'
set shiftwidth=4   " number of spaces to use in each (auto)indent
set smarttab       " shiftwidth instead of tabstop at linestart
set softtabstop=4  " backspace through spaced tabs
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1

" Basic Keybinds {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"- Leader key mapping; view all leader keys with ':g/<leader>/'
"- Unmap default <Space> key bind
nnoremap <Space> <nop>
"- Using 'map <Space> <leader>' to also retain default leader of '\'
map <Space> <leader>
 
"- Buffer movement
nnoremap <C-n> :bn<CR>
nnoremap <C-p> :bp<CR>
nnoremap <leader>d :bdelete<CR>
nnoremap <leader>o :only<CR>
nnoremap <leader>c :close<CR>
 
"- Rebind arrow keys to resize splits
nnoremap <Left> 1<C-w><
nnoremap <right> 1<C-w>>
nnoremap <up> 1<C-w>+
nnoremap <down> 1<C-w>-
 
"- Make these commands behave intuitively
nnoremap gg gg0
nnoremap G G0
nnoremap Y y$
nnoremap & :&&<CR>

"- Command line
"- Expand current path using %%
cnoremap %% <C-r>=expand('%:p:h')<CR>/
"- Bash shell command line movements
cnoremap <C-b> <Left>
cnoremap <C-f> <Right>
cnoremap <C-a> <Home>
cnoremap <C-d> <Delete>
"- Replace <C-f> for opening command line editor window
set cedit=<C-y>
"- Quickly edit macros e.g. '"<register><leader>m'; must match cedit key bind
nnoremap <leader>m :<C-u><C-r>= 'let @'. v:register .' = '. string(getreg(v:register))<CR><C-e>
 
" File explore
nnoremap <leader>e :Explore<CR>

"- Toggle visualization of character returns and tabs
nnoremap gl :set list!<CR>
 
"- Spell check
"- Toggle spelling
nnoremap cos :set spell!<CR>:set spell?<CR>
"- Instantly-correct spelling (or use qs1z=q then @s then @@)
nnoremap [S 1z=
nnoremap ]S 1z=

"- Toggle relative line number
nnoremap <leader>n :setlocal number! relativenumber!<CR>
 
"- Toggle paste
nnoremap <leader>p :set paste!<CR>:set paste?<CR>
"- Make sure X11 server is enabled for this:
nnoremap <leader>P :set paste<CR>"+gP:set nopaste<CR>

"- Select pasted text; overrides useless select-mode bind
nnoremap gV `[V`]

" Turn off highlighted values with '<leader>l' and 'coh'
nnoremap <leader>l :nohlsearch<CR><C-l>
nnoremap coh :nohlsearch<CR><C-l>

"- Utility command for substitution
nnoremap <leader>; :%s///ge<Left><Left><Left>
vnoremap <leader>; :s///ge<Left><Left><Left>
nmap g: <leader>;
vmap g: <leader>;

"- Utility commands to input spaces
"- Insert spaces around character
nnoremap css i <Esc>la <Esc>h
nnoremap csh i <Esc>l
nnoremap csl a <Esc>h
"- Insert spaces around line
nnoremap csk mmO <Esc>`m:delmarks m<CR>
nnoremap csj mmo <Esc>`m:delmarks m<CR>

"- Vim RC commands
nnoremap <leader>v :e $MYVIMRC<CR>
nnoremap <leader>r :source %<CR>
nnoremap <leader>R :source $MYVIMRC<CR>

"- Vertical movement - move down in same column based on editor lines
nnoremap j gj
nnoremap k gk
nnoremap gj j
nnoremap gk k
 
"- Save file as root
cmap w!! :w !sudo tee > /dev/null %<CR>

"- Quickfix and file searching
"- Open quickfix window
nnoremap <leader>q :cwindow<CR>
"- Clear and close quickfix list
nnoremap <leader>Q :cexpr []<CR>:cclose<CR>
"- Search files
nnoremap <leader>/ :<C-u>vimgrep //j **/*<Left><Left><Left><Left><Left><Left><Left>
nnoremap <leader>g/ :<C-u>cexpr []<CR>:<C-u>noautocmd bufdo vimgrepadd //j %<Left><Left><Left><Left>
nnoremap <leader>* :<C-u>noautocmd vimgrep /<C-r><C-w>/j **/*<CR>
nnoremap <leader>g* :<C-u>cexpr []<CR>:<C-u>noautocmd bufdo vimgrepadd /<C-r><C-w>/j %<CR>
vnoremap <leader>* :<C-u>noautocmd vimgrep /<C-r><C-w>/j **/*<CR>
vnoremap <leader>g* :<C-u>cexpr []<CR>:<C-u>noautocmd bufdo vimgrepadd /<C-r><C-w>/j %<CR>
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1

" Abbreviations {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"- :(ic)ab[breviate], i for insert mode, c for command line mode
ab date>> <C-r>=strftime('%Y-%m-%d')<CR>
ab time>> <C-r>=strftime('%H:%M')<CR>
iab TODO>> TODO <C-r>=strftime('%F')<CR>
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1
 
" Appearance {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"- To check set appearances, use ':verbose hi'
"- Search online for 'xterm 256 colors' to see visual array
if !exists('g:loaded_plug')  " if plugins are disabled
    highlight ColorColumn ctermbg=255
endif
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1
 
" Commands {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"- Full screen help
command! -nargs=1 -complete=help H help <args> | silent only
"- Aliases
command! Vrc :edit $MYVIMRC
command! Inst :source $MYVIMRC | :PluginInstall
command! Tco :edit ~/.tmux.conf
command! Bma :edit ~/.bash_matt
command! Brc :edit ~/.bashrc
command! Bpr :edit ~/.bash_profile
command! Bal :edit ~/.bash_aliases
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1
 
" Terminal Mode {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" For help :h :term
" Remap <Esc> to change terminal to normal mode
if v:version >= 802
  tnoremap <Esc> <C-W>N
endif
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1

" Advanced {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"- Turn on folds for vim file-types; specify desired depth
autocmd FileType vim setlocal foldmethod=marker foldlevel=1
 
"- Search for selected text, forwards or backwards
"- See https://vim.fandom.com/wiki/Search_for_visually_selected_text
vnoremap <silent> * :<C-u>
  \ let old_reg=getreg('"')<Bar>let old_regtype=getregtype('"')<CR>
  \ gvy/<C-r>=&ic?'\c':'\C'<CR><C-r><C-r>=substitute(
  \ escape(@", '/\.*$^~['), '\_s\+', '\\_s\\+', 'g')<CR><CR>
  \ gVzv:call setreg('"', old_reg, old_regtype)<CR>
vno remap <silent> # :<C-u>
  \ let old_reg=getreg('"')<Bar>let old_regtype=getregtype('"')<CR>
  \ gvy?<C-r>=&ic?'\c':'\C'<CR><C-r><C-r>=substitute(
  \ escape(@", '?\.*$^~['), '\_s\+', '\\_s\\+', 'g')<CR><CR>
  \ gVzv:call setreg('"', old_reg, old_regtype)<CR>
 
"- When editing a file, always jump to the last known cursor position
augroup enterLastPos
  autocmd!
  autocmd BufReadPost *
    \ if line("'\"") > 1 && line("'\"") <= line("$") |
    \   execute "normal! g`\"zz" |
    \ endif
augroup END

"- Save last session on every write, use 'vim -S ~/.vim/lastSession.vim'
augroup fileExit
  autocmd!
  autocmd BufWritePost *
  \ execute "mksession! ~/.vim/lastSession.vim"
augroup END
 
"- Run macro over visual selection (alternative to '<,'>normal @q)
xnoremap @ :<C-u>call ExecuteMacroOverVisualRange()<CR>
function! ExecuteMacroOverVisualRange()
  echo "@".getcmdline()
  execute ":'<,'>normal @".nr2char(getchar())
endfunction
 
"- Use literal tabs in a make file
augroup makeFiles
  autocmd!
  autocmd FileType make set noexpandtab
augroup END
 
"- Detect OpenFOAM filetype
function! SetFoamFileType()
  for nL in range(1,10)  " loop through the first 10 lines
    if (getline(nL) =~ 'FoamFile' || getline(nL) =~ 'F ield')
      setfiletype cpp  " cpp, foam
      break
    endif
  endfor
endfunction
augroup FOAMCmds
  autocmd!
  autocmd BufRead * call SetFoamFileType()
augroup End

" Source Vimscript without sourcing entire file; or yank and :@<reg>
function! SourceVimscript(type)
  let sel_save = &selection
  let &selection = "inclusive"
  let reg_save = @"
  if a:type == 'line'
    silent execute "normal! '[V']y"
  elseif a:type == 'char'
    silent execute "normal! `[v`]y"
  elseif a:type == "visual"
    silent execute "normal! gvy"
  elseif a:type == "currentline"
    silent execute "normal! yy"
  endif
  let @" = substitute(@", '\n\s*\\', '', 'g')
  " source the content
  @"
  let &selection = sel_save
  let @" = reg_save
endfunction
nnoremap g: :set opfunc=SourceVimscript<cr>g@
vnoremap g: :<c-U>call SourceVimscript("visual")<cr>
nnoremap g:: :call SourceVimscript("currentline")<cr>
 
"- Turn on spell check for certain file types
augroup enableSpellCheck
  autocmd!
  autocmd BufRead *.txt,*.tex,*.md,*.rst setlocal spell
    \ | syntax spell toplevel
augroup END
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1

" Tips and Tricks {{{1
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"- Using Vim from the Command Line
"------------------------------------------------------------------------------
"- -c or +  for executing :commands with | inbetween commands
"- -s       for executing normal-mode commands

"- Delete all lines with foo in them from command line
"-   vi -u NONE -N -c "v/foo/d|wq" fooAndBar.txt
"-   vi -u NONE -N +"v/foo/d|wq" fooAndBar.txt
"-   vi -u NONE -N -s normalCmdsScript.vim fooAndBar.txt

"- In normalCmdsScript.vim
"-   :v/foo/d<CR>:wq<CR>

"- In fooAndBar.txt
"-   This line has a bar and a foo
"-   This line only has a bar
"-   This line only has a foo

"- In fooAndBar.txt after modification
"-   This line only has a bar
"------------------------------------------------------------------------------

"- In-File Tips
"------------------------------------------------------------------------------
"- Substitution:
"- Use :cfdo %s/SEARCH/REPLACE/ge to modify all files in the quickfix list
"-  See also :bufdo, :argdo, and :windo
"- Use :args ## % to append current file to arglist
"- Use & in normal model to repeat substitution
"- Use \=str2float(submatch(1)) instead of \1 when using :s with math

"- Using builtin file find:
"- set path+=.,**
"- :find name*<tab>

"- Scripting with Python within Vim:
"- Remove comment tag on the Python command and type :.!python3 - 
"- Or highlight line(s) then !python3
"- for i, x in enumerate("ABC"): print("Person %i is %s"%(i,x))

"- List all lines where the word under the cursor appears: [I or [i for first
 
"- Source Vimscript code without sourcing entire file: yank code and :@0

"- View current vim environement:
"-   https://vim.fandom.com/wiki/Displaying_the_current_Vim_environment
"- View message outputs in separate buffer:
"-   :redir @r | let g:
"-   :new | put r | redir END
"------------------------------------------------------------------------------
 
"- Easter eggs
"------------------------------------------------------------------------------
"- :Ni!
"- :h 42
"- :h UserGettingBored
"- :h holy-grail
"- :smile
"- :h help!
"------------------------------------------------------------------------------
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" }}}1
